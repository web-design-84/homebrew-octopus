#!/bin/zsh

#-------------------------------
# octopus プロジェクトを生成する
#-------------------------------

function octopus_init() {
  # 必須コマンドがなければインストールを促す
  if [[ ! $(command -v docker) ]]; then
    echo 'octopus コマンドを実行するには、 docker が必要です。'
    echo '下記コマンドを実行し、 docker をインストールしてください。\n'
    echo 'brew install --cask docker\n'
    exit 1
  fi

  if [[ ! $(command -v mkcert) ]]; then
    echo 'octopus コマンドを実行するには、 mkcert が必要です。'
    echo '下記コマンドを実行し、 mkcert をインストールしてください。\n'
    echo 'brew install mkcert'
    echo 'mkcert -install\n'
    exit 1
  fi

  if [[ ! $(command -v wp) ]]; then
    echo 'octopus コマンドを実行するには、 WP-CLI が必要です。'
    echo '下記コマンドを実行し、 WP-CLI をインストールしてください。\n'
    echo 'curl -O https://raw.githubusercontent.com/wp-cli/builds/gh-pages/phar/wp-cli.phar'
    echo 'php wp-cli.phar --info'
    echo 'chmod +x wp-cli.phar'
    echo 'sudo mv wp-cli.phar /usr/local/bin/wp\n'
    exit 1
  fi

  if [[ ! $(command -v pnpm) ]]; then
    echo 'octopus コマンドを実行するには、 pnpm が必要です。'
    echo '下記コマンドを実行し、 pnpm をインストールしてください。\n'
    echo 'brew install pnpm\n'
    exit 1
  fi

  # GitHubホスト名の確認＆登録
  if [ -z $(git config --global ssh.host) ]; then
    echo -n 'WEB DESIGN 84 GitHub SSH Host > '
    read github_ssh_host
    if [ -z "$github_ssh_host" ]; then
      echo 'GitHubのSSHホスト名（~/.ssh/config に記述）を入力してください。'
      exit 1
    elif [[ ! $(grep $github_ssh_host ~/.ssh/config) ]]; then
      echo '~/.ssh/config にホストが登録されていません。'
      exit 1
    fi
    git config --global ssh.host $github_ssh_host
  fi

  # プロジェクト名
  echo -n 'Project Name > '
  read project_name
  if [ -z "$project_name" ]; then
    echo 'プロジェクト名を入力してください。'
    exit 1
  elif [ -d $HOME/projects/$project_name ]; then
    echo '既にプロジェクトが存在します。他の名前をお試しください。'
    exit 1
  fi

  # サイト名
  echo -n 'Site Title > '
  read site_title
  if [ -z "$site_title" ]; then
    echo 'サイト名を入力してください。'
    exit 1
  fi

  # ドメイン名
  echo -n 'Domain > '
  read domain
  if [ -z "$domain" ]; then
    echo 'ドメイン名を入力してください。'
    exit 1
  elif grep -q "$domain" /etc/hosts; then
    echo "'$domain' はすでに存在します。"
    echo '/etc/hosts からドメインを削除するか、他のドメインを入力してください。'
    exit 1
  fi

  # ホストIP
  for i in {1..20}; do
    if ! grep -q "127.0.0.$i" /etc/hosts; then
      local ip="127.0.0.$i"
      break
    fi
  done
  if [ -z "$ip" ]; then
    echo 'ローカルループバックアドレスに空きがありません。'
    echo '新たに確保するか、不使用のプロジェクトを整理してください。'
    exit 1
  fi

  # 確認後、プロジェクトの作成
  echo '下記の内容でプロジェクトを作成します。よろしいですか（y/n）'
  echo '--------------------------'
  echo "プロジェクト名：$project_name"
  echo "サイト名：$site_title"
  echo "ドメイン：$domain"
  echo "ホストIPアドレス：$ip"
  echo '--------------------------'
  if ! read -sq; then
    echo 'Octopusプロジェクトの作成をキャンセルしました。'
    exit
  fi

  # ~/projects が存在しない場合、ホームディレクトリへ作成する
  if [ ! -d $HOME/projects ]; then
    mkdir $HOME/projects
  fi

  # ~/projects/$project_name/dev にOctopusをインストール
  cd $HOME/projects
  mkdir $project_name && cd $project_name
  git clone $(git config --global ssh.host):web-design-84/Octopus.git dev
  if [ ! -d $HOME/projects/$project_name/dev ]; then
    echo 'クローンに失敗しました。'
    echo 'GitHubのホスト名を見直してください。'
    exit 1
  fi

  # 初期設定
  cd dev && rm -rf .git && mkdir {sql/db_data,mailhog}
  pnpm i

  # .env の書き換え
  sed -i '' -e "s/^PRODUCTION_NAME=.*$/PRODUCTION_NAME=$project_name/" -e "s/^SITE_TITLE=.*$/SITE_TITLE='$site_title'/" -e "s/^DOMAIN=.*$/DOMAIN='$domain'/" -e "s/^HOST_IP=.*$/HOST_IP=$ip/" .env

  # 証明書の作成
  mkdir certs
  mkcert -cert-file ./certs/cert.pem -key-file ./certs/cert.key $domain

  # WordPress日本語版コアファイルのダウンロード
  wp core download --locale=ja

  # コンテナの立ち上げ
  docker compose -p $project_name up -d --build

  # 立ち上がりを待つ
  echo 'コンテナ起動の安定を待っています。30秒お待ちください🐙'
  for i in {1..10}; do
    # 進捗10%あたり "###" を出力
    local bar="$(yes '###' | head -n $i | tr -d '\n')"
    local spaces=''
    if [[ $i < 10 ]]; then
      local spaces=$(printf ".%0.s" {1..$((30 - ${#bar}))})
    fi
    printf "\r[%3d/100] %s%s" $((i * 10)) $bar $spaces
    sleep 3
  done
  printf '\n'

  # WP-CLIによるWordPressセットアップ
  docker exec "${project_name}_dev_wp" /bin/zsh setup-wp.sh

  # /etc/hosts にホストを追加
  echo 'honoka' | sudo -S -p '' zsh -c "echo \"# $site_title\n$ip $domain\n\" >> /etc/hosts"

  # 完了
  echo "~/projects/$project_name/dev へOctopusプロジェクトを作成しました！"
  exit
}

#-------------------------------
# octopus プロジェクトを削除する
#-------------------------------

function octopus_destroy() {
  echo -n 'Project Name > '
  read project_name
  if [ -z "$project_name" ]; then
    echo 'プロジェクト名を入力してください。'
    exit 1
  elif [ ! -d $HOME/projects/$project_name ]; then
    echo 'プロジェクトが存在しません。'
    exit 1
  fi

  echo "$HOME/projects/$project_name 以下のファイルを全て削除します。本当によろしいですか？（y/n）"
  if ! read -sq; then
    echo 'プロジェクトの破棄をキャンセルしました。'
    exit
  fi

  # Dockerコンテナの削除
  if [[ $(docker compose ls | awk 'NR>1{print $1}' | grep $project_name) ]]; then
    cd $HOME/projects/$project_name/dev
    docker compose -p $project_name down
  fi

  # Dockerイメージの削除
  if [[ $(docker images | awk 'NR>1{print $1}' | grep ${project_name}_wordpress) ]]; then
    docker rmi -f ${project_name}_wordpress
  fi

  # /etc/hosts から割り当てIPを削除
  local host_ip=$(ggrep -oP '127\.0\.0\.\d+$' $HOME/projects/$project_name/dev/.env)
  local line_count=$(ggrep -noP "^$host_ip" /etc/hosts | awk -F ':' '{print $1}')
  local delete_line_start=$( (expr $line_count - 1))
  local delete_line_end=$( (expr $line_count + 1))
  echo 'honoka' | sudo -S -p '' zsh -c "sed -i '' \"${delete_line_start},${delete_line_end}d\" /etc/hosts"

  # ディレクトリの削除
  cd $HOME/projects
  rm -rf $HOME/projects/$project_name

  echo 'Octopusプロジェクトの破棄が完了しました。'
}

#-------------------------------
# octopus テーマをzipでダウンロードする
#-------------------------------

function octopus_download() {
  # GitHubホスト名の確認＆登録
  local github_ssh_host=$(git config --global ssh.host)
  if [[ -z $github_ssh_host ]]; then
    echo -n 'WEB DESIGN 84 GitHub SSH Host > '
    read github_ssh_host
    if [ -z "$github_ssh_host" ]; then
      echo 'GitHubのSSHホスト名（~/.ssh/config に記述）を入力してください。'
      exit 1
    elif [[ ! $(grep $github_ssh_host ~/.ssh/config) ]]; then
      echo '~/.ssh/config にホストが登録されていません。'
      exit 1
    fi
    git config --global ssh.host $github_ssh_host
  fi

  # tmp ファイルへ octopus をクローン
  mkdir octopus-tmp && cd octopus-tmp
  git clone ${github_ssh_host}:web-design-84/octopus.git octopus-all

  # テーマファイルのみ抽出し、zipファイルを ~/Downloadsに生成
  mv octopus-all/wp-content/themes/octopus/ .
  rm -rf octopus-all
  zip -r octopus-theme.zip octopus/
  mv -f octopus-theme.zip ~/Downloads
  cd ../ && rm -rf octopus-tmp
}

#-------------------------------
# octopus コマンドのヘルプを表示する
#-------------------------------

function octopus_help() {
  echo '
  ■ Sub commands
  ━━━━━━━━━━━━━━━━
  init      ~/projects へOctopusプロジェクトを作成する
  destroy   ~/projects のOctopusプロジェクトを破棄する
  download  ~/Downloads へ最新バージョンのOctopusテーマをダウンロードする

  ■ Options
  ━━━━━━━━━━━━━━━━
  -v        バージョン
  --help    ヘルプ
  '
  exit
}

#-------------------------------
# octopus コマンドのバージョンを表示する
#-------------------------------

function octopus_version() {
  echo 'version 1.1.3'
  exit
}

#-------------------------------
# サブコマンド、オプションの振り分け
#-------------------------------

while getopts vh:p:-: OPT; do
  case $OPT in
  v) octopus_version ;;
  -)
    case "${OPTARG}" in
    help) octopus_help ;;
    esac
    ;;
  esac
done

subcommand="$1"

case $subcommand in
init) octopus_init ;;
destroy) octopus_destroy ;;
download) octopus_download ;;
*)
  echo "ヘルプを参照するには、 'octopus --help' を実行してください。"
  exit 1
  ;;
esac
